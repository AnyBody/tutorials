/**
This model demonstrates the AnyProject and related classes such as task and view definitions.

Task examples:
- Running typical model operations from studies, such as setting initial conditions and inverse dynamics
- Loading another model
- Executing class operations such saving or loading from the simulation


Model View definitions:

*/
Main = {
  #include "Demo.arm2d.any"
  #include "DrawGroups.any"
  
  
  AnyProject ArmProject = {
    
    Tasks = {
      
      AnyProjectTaskOperation Set_Initial_Conditions = {
        Description = {
          Title = "Initial Conditions of Arm Model";
          BodyText = "This task sets the initial conditions.";
          Tooltip = "Sets Initial Conditions";
          Files = {"dumbbell.stl","values.anyset"};
        };
        Operation = &Main.ArmModelStudy.InitialConditions;
      };
      
      AnyProjectTaskOperation Run_Inverse_Dynamics = {
        Description = {
          Title = "Inverse Dynamcis of Arm Model";
          BodyText = "This task executes the inverse dynamics analysis of the arm model.";
          Tooltip = "Inverse Dynamics Simulation";
        };
        Operation = &Main.ArmModelStudy.InverseDynamics;
      };

      AnyProjectTaskOperation Save_Output_Data = {
        Description = {
          Title = "Saving output data from the simulation";
          BodyText = "This task saves the output for the simulation, e.g. executed by task " +
          strquote( AnyBodyLinkOf(&..Run_Inverse_Dynamics) )
          + " to file " 
          + strquote( strlink(.filename) )
          ;
          Tooltip = "Saving output data";
          Files = .filename;
        };
        Operation = &Opr;
        
        AnyFileVar filename = FileNameOf(..Files.MainFile) + ".anydata.h5";
        
        AnyOperationMacro Opr = {
          MacroStr = {
            //"classoperation Main.ArmStudy1.Output" + " " + strquote("Save data"),
            ("classoperation Main.ArmStudy1.Output" + " " + strquote("Save data")
            + " --file=" + strquote(FilePathCompleteOf(.filename)) + " --type=Deep")
          };
        };
      };

      
      AnyProjectTaskOperation Load_Output_Data = {
        Description = {
          Title = "Loading output data from a previous simulation";
          BodyText = "This task load output saved by the task " +
          strquote( AnyBodyLinkOf(&..Save_Output_Data) )
          + " from file " 
          + strquote( strlink(.filename) )
          ;
          Tooltip = "Loading output data";
          //Files = .filename;
        };
        Operation = &Opr;
        AnyFileVar filename = .Save_Output_Data.filename;
        
        
        AnyOperationMacro Opr = {
          MacroStr = {
            "classoperation Main.ArmStudy1.Output" + " " + strquote("Load data")
            + " --file=" + strquote(FilePathCompleteOf(.filename))
          };
        };
      };
      
      AnyProjectTaskLoadModel Load_the_model_without_project = {
        MainFile = "Demo.arm2d.any";
      };
    };
    
    Views = {
      
      AnyProjectModelViewDefinition View_All = {
        DrawGroupSequence = {&Main.DrawGroups.All};
        ShowModelDefined = {On};
      };
      
      AnyProjectModelViewDefinition View_deltoid_selection = {
        DrawGroupSequence = {&Main.DrawGroups.AllSegments, &Main.DrawGroups.AllMuscles, &Main.DrawGroups.AllDeltoidMuscles};
        Reset = {Off, Off, Off}; //Same behavior as not defining Reset.
        Hide = {Off, Off, Off}; //Same behavior as not defining Hide.
        ShowModelDefined = {On, Off, On}; 
        ShowAutoGenerated = {Off, On, Off}; //Same behavior as {Off, On}.
        Transparent = {On, Off, On}; 
        Select = {Off, Off, Off}; //Same behavior as not defining Select.
      };
      
      AnyProjectModelViewDefinition View_deltoid_selection2 = {
        DrawGroupSequence = {&Main.DrawGroups.AllSegments, &Main.DrawGroups.AllMuscles, &Main.DrawGroups.AllDeltoidMuscles};
        Reset = {On};
        ShowModelDefined = {Off,On};
        Transparent = {Off,Off,On};
        Select = {Off,Off,On};
      };
      
      AnyProjectModelViewDefinition View_select_all = {
        DrawGroupSequence = {&Main.DrawGroups.AllSegments, &Main.DrawGroups.AllMuscles};
        ShowModelDefined = {On};
        Select = {On};
      };
    };
    
    Files = {
      MainFile = "AnyProject.any";

      SetValueFile = "values.anyset";
    };
  };
  
};  // Main