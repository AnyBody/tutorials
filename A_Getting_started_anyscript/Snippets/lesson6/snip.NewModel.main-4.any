// Todo: Write a small description of your model here

Main = {
  // The actual body model goes in this folder
  AnyFolder ArmModel = {
  
    // Global Reference Frame
    AnyFixedRefFrame GlobalRef = {
      viewRefFrame.Visible = On;
      viewRefFrame.ScaleXYZ = {0.1, 0.1, 0.1};
      viewRefFrame.RGB = {0,1,0};

      AnyRefNode Shoulder = {
        sRel = {0,0,0};
      };
      AnyRefNode DeltodeusA = {
        sRel = {0.05,0,0};
      };
      AnyRefNode DeltodeusB = {
        sRel = {-0.05,0,0};
      };
      AnyRefNode BicepsLong = {
        sRel = {0.1,0,0};
      };
      AnyRefNode TricepsLong = {
        sRel = {-0.1,0,0};
      };
    }; // Global Reference Frame

    // Segments
    AnyFolder Segs = {
      
      //UpperArm
      AnySeg UpperArm = {
        r0 = {0.0, 0.3, 0.0};
        Axes0 = RotMat(-90*pi/180, z);
        Mass = 2;
        Jii = {0.001, 0.01, 0.01};
        viewRefFrame.Visible = On;
        viewNodes.Visible = On;
        viewInertia.Visible = On;

        AnyRefNode ShoulderNode = {
          sRel = {-0.2,0,0};
        };
        AnyRefNode ElbowNode = {
          sRel = {0.2,0,0};
        };
        AnyRefNode DeltodeusA = {
          sRel = {-0.1,0,0.02};
        };
        AnyRefNode DeltodeusB = {
          sRel = {-0.1,0,-0.02};
        };
        AnyRefNode Brachialis = {
          sRel = {0.1,0,0.01};
        };
        AnyRefNode BicepsShort = {
          sRel = {-0.1,0,0.03};
        };
        AnyRefNode Brachioradialis = {
          sRel = {0.05,0,0.02};
        };
        AnyRefNode TricepsShort = {
          sRel = {-0.1,0,-0.01};
        };
      }; //UpperArm

      // ForeArm
      AnySeg ForeArm = {
        r0 = {0.3, 0.0, 0.0};
        Mass = 2.0;
        Jii = {0.001,0.01,0.01};
        viewRefFrame.Visible = On;
        viewNodes.Visible = On;
        viewInertia.Visible = On;

        AnyRefNode ElbowNode = {
          sRel = {-0.2,0,0};
        };
        AnyRefNode HandNode = {
          sRel = {0.2,0,0};
        };
        AnyRefNode Brachialis = {
          sRel = {-0.1,0,0.02};
        };
        AnyRefNode Brachioradialis = {
          sRel = {0.0,0,0.02};
        };
        AnyRefNode Biceps = {
          sRel = {-0.15,0,0.01};
        };
        AnyRefNode Triceps = {
          sRel = {-0.25,0,-0.05};
        };
//# BEGIN SNIPPET 1
AnyRefNode PalmNode = {
  sRel = {0.27,0,0};
  §ARel = RotMat(90*pi/180, y);§
  AnyDrawSTL DrwSTL = {
    FileName = "dumbbell.stl";
    ScaleXYZ = {0.001, 0.001, 0.001};
  };
};
//# END SNIPPET 1
      }; // ForeArm
    }; // Segs folder

    // Joints 
    AnyFolder Jnts = {
      //---------------------------------
      AnyRevoluteJoint Shoulder = {
        AnyRefNode &GroundNode = ..GlobalRef.Shoulder;
        AnyRefNode &UpperArmNode = ..Segs.UpperArm.ShoulderNode;
        Axis = z;
      }; // Shoulder joint

      AnyRevoluteJoint Elbow = {
        Axis = z;
        AnyRefNode &UpperArmNode = Main.ArmModel.Segs.UpperArm.ElbowNode;
        AnyRefNode &ForeArmNode = Main.ArmModel.Segs.ForeArm.ElbowNode;
      }; // Elbow joint
    }; // Jnts folder

    AnyFolder Drivers = {
      //---------------------------------
      AnyKinEqSimpleDriver ShoulderMotion = {
        AnyRevoluteJoint &Jnt = ..Jnts.Shoulder;
        DriverPos = {-100*pi/180};
        DriverVel = {30*pi/180};
        Reaction.Type = {Off};
      }; // Shoulder driver
      //---------------------------------
      AnyKinEqSimpleDriver ElbowMotion = {
        AnyRevoluteJoint &Jnt = ..Jnts.Elbow;
        DriverPos = {90*pi/180};
        DriverVel = {45*pi/180};
        Reaction.Type = {Off};
      }; // Elbow driver 
    }; // Driver folder

    AnyFolder Muscles = {
      // Simple muscle model with constant strength = 400 Newton
      AnyMuscleModel MusMdl = {
        F0 = 400;
      };

      //---------------------------------
      AnyMuscleViaPoint Brachialis = {
        AnyMuscleModel &MusMdl = ..Muscles.MusMdl;
        AnyRefNode &Org = ..Segs.UpperArm.Brachialis;
        AnyRefNode &Ins = ..Segs.ForeArm.Brachialis;
        viewMuscle.Visible = On;
      };

      //---------------------------------
      AnyMuscleViaPoint DeltodeusA = {
        AnyMuscleModel &MusMdl = ..Muscles.MusMdl;
        AnyRefNode &Org = ..GlobalRef.DeltodeusA;
        AnyRefNode &Ins = ..Segs.UpperArm.DeltodeusA;
        viewMuscle.Visible = On;
      };

      //---------------------------------
      AnyMuscleViaPoint DeltodeusB = {
        AnyMuscleModel &MusMdl = ..Muscles.MusMdl;
        AnyRefNode &Org = ..GlobalRef.DeltodeusB;
        AnyRefNode &Ins = ..Segs.UpperArm.DeltodeusB;
        viewMuscle.Visible = On;
      };

      //---------------------------------
      AnyMuscleViaPoint Brachioradialis = {
        AnyMuscleModel &MusMdl = ..Muscles.MusMdl;
        AnyRefNode &Org = ..Segs.UpperArm.Brachioradialis;
        AnyRefNode &Ins = ..Segs.ForeArm.Brachioradialis;
        viewMuscle.Visible = On;
      };

      //---------------------------------
      AnyMuscleViaPoint BicepsShort = {
        AnyMuscleModel &MusMdl = ..Muscles.MusMdl;
        AnyRefNode &Org = ..Segs.UpperArm.BicepsShort;
        AnyRefNode &Ins = ..Segs.ForeArm.Biceps;
        viewMuscle.Visible = On;
      };

      //---------------------------------
      AnyMuscleViaPoint TricepsShort = {
        AnyMuscleModel &MusMdl = ..Muscles.MusMdl;
        AnyRefNode &Org = ..Segs.UpperArm.TricepsShort;
        AnyRefNode &Ins = ..Segs.ForeArm.Triceps;
        viewMuscle.Visible = On;
      };

      //---------------------------------
      AnyMuscleViaPoint BicepsLong = {
        AnyMuscleModel &MusMdl = ..Muscles.MusMdl;
        AnyRefNode &Org = ..GlobalRef.BicepsLong;
        AnyRefNode &Ins = ..Segs.ForeArm.Biceps;
        viewMuscle.Visible = On;
      };

      //---------------------------------
      AnyMuscleViaPoint TricepsLong = {
        AnyMuscleModel &MusMdl = ..Muscles.MusMdl;
        AnyRefNode &Org = ..GlobalRef.TricepsLong;
        AnyRefNode &Ins = ..Segs.ForeArm.Triceps;
        viewMuscle.Visible = On;
      };
    }; // Muscles folder

    AnyFolder Loads = {
    //---------------------------------
      AnyForce3D Dumbbell = {
        AnyRefNode &PalmNode = ..Segs.ForeArm.PalmNode;
        F = {0,-100,0}; // Force in Newton
      };
    }; // Loads folder

  };// ArmModel
  
  // The study: Operations to be performed on the model
  AnyBodyStudy ArmStudy = {
    AnyFolder &Model = .ArmModel;
    Gravity = {0.0, -9.81, 0.0};
  };
}; 